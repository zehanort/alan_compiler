%{
#include <stdio.h>
#include <stdlib.h>
#include "codegen.hpp"

using namespace std;

void yyerror (const char *msg);

extern int yylex();
extern char *yytext;
extern int linecount;
const char *filename;
ASTNode *t;
%}

%union{
	ASTNode *a;
	char c;
	char *s;
	int n;
	Type t;
}

%token T_byte "byte"
%token T_else "else"
%token T_false "false"
%token T_if "if"
%token T_int "int"
%token T_proc "proc"
%token T_reference "reference"
%token T_return "return"
%token T_while "while"
%token T_true "true"
%token T_eq "=="
%token T_ne "!="
%token T_le "<="
%token T_ge ">="
%token<s> T_id
%token<n> T_const
%token<c> T_char
%token<s> T_string

%left '|'
%left '&'
%left T_eq T_ne '>' '<' T_le T_ge
%left '+' '-'
%left '*' '/' '%'
%left '!'
%left UMINUS UPLUS

%type<a> program
%type<a> func-def
%type<a> fpar-list
%type<a> fpar-def
%type<a> local-def-list
%type<a> local-def
%type<t> type
%type<t> data-type
%type<t> r-type
%type<a> var-def
%type<a> compound-stmt
%type<a> stmt-list
%type<a> stmt
%type<a> func-call
%type<a> expr-list
%type<a> expr
%type<a> l-value
%type<a> cond

%%

program:
	func-def { t = $$ = $1; }
;

func-def:
	T_id '(' fpar-list ')' ':' r-type local-def-list compound-stmt { $$ = new ASTFdef(new ASTFdecl($1, $6, $3, $7), $8); }
;

fpar-list:
	/* nothing */ { $$ = NULL; }
|	fpar-def { $$ = new ASTSeq($1, NULL); }
|	fpar-def ',' fpar-list { $$ = new ASTSeq($1, $3); }
;

fpar-def:
	T_id ':' type { $$ = new ASTPar($1, $3, PASS_BY_VALUE); }
|	T_id ':' "reference" type { $$ = new ASTPar($1, $4, PASS_BY_REFERENCE); }
;

local-def-list:
	/* nothing */ { $$ = NULL; }
|	local-def local-def-list { $$ = new ASTSeq($1, $2); }
;

local-def:
	func-def { $$ = $1; }
|	var-def { $$ = $1; }
;

type:
	data-type { $$ = $1; }
|	data-type '[' ']' { $$ = typeIArray($1); }
;

data-type:
	"int" { $$ = typeInteger; }
|	"byte" { $$ = typeChar; }
;

r-type:
	data-type { $$ = $1; }
|	"proc" { $$ = typeVoid; }
;

var-def:
	T_id ':' data-type ';' { $$ = new ASTVdef($1, $3, 0); }
|	T_id ':' data-type '[' T_const ']' ';' { $$ = new ASTVdef($1, $3, $5); }
;

compound-stmt:
	'{' stmt-list '}' { $$ = $2; }
;

stmt-list:
	/* nothing */ { $$ = new ASTSeq(NULL, NULL); }
|	stmt stmt-list { $$ = new ASTSeq($1, $2); }
;

stmt:
	';' { $$ = NULL; }
|	l-value '=' expr ';' { $$ = new ASTAssign($1, $3); }
|	compound-stmt { $$ = $1; }
|	func-call ';' { $$ = new ASTFcall_stmt($1); }
|	"if" '(' cond ')' stmt { $$ = new ASTIf($3, $5); }
|	"if" '(' cond ')' stmt "else" stmt { $$ = new ASTIfelse(new ASTIf($3, $5), $7); }
|	"while" '(' cond ')' stmt { $$ = new ASTWhile($3, $5); }
|	"return" ';' { $$ = new ASTRet(NULL); }
|	"return" expr ';' { $$ = new ASTRet($2); }
;

func-call:
	T_id '(' expr-list ')' { $$ = new ASTFcall($1, $3); }
;

expr-list:
	/* nothing */ { $$ = NULL; }
|	expr { $$ = new ASTSeq($1, NULL); }
|	expr ',' expr-list { $$ = new ASTSeq($1, $3); }
;

expr:
	T_const { $$ = new ASTInt($1); }
|	T_char { $$ = new ASTChar($1); }
|	l-value { $$ = $1; }
|	'(' expr ')' { $$ = $2; }
|	func-call { $$ = $1; }
|	expr '+' expr { $$ = new ASTOp($1, PLUS, $3); }
|	expr '-' expr { $$ = new ASTOp($1, MINUS, $3); }
|	expr '*' expr { $$ = new ASTOp($1, TIMES, $3); }
|	expr '/' expr { $$ = new ASTOp($1, DIV, $3); }
|	expr '%' expr { $$ = new ASTOp($1, MOD, $3); }
|	'+' expr { $$ = new ASTOp(new ASTInt(0), PLUS, $2); }		%prec UPLUS
|	'-' expr { $$ = new ASTOp(new ASTInt(0), MINUS, $2); }	%prec UMINUS
;

l-value:
	T_id { $$ = new ASTId($1, NULL); }
|	T_id '[' expr ']' { $$ = new ASTId($1, $3); }
|	T_string { $$ = new ASTString($1); }
;

cond:
	"true" { $$ = new ASTInt(1); }
|	"false" { $$ = new ASTInt(0); }
|	'(' cond ')' { $$ = $2; }
|	'!' cond { $$ = new ASTOp(NULL, NOT, $2); }
|	expr "==" expr { $$ = new ASTOp($1, EQ, $3); }
|	expr "!=" expr { $$ = new ASTOp($1, NE, $3); }
|	expr '<' expr { $$ = new ASTOp($1, LT, $3); }
|	expr '>' expr { $$ = new ASTOp($1, GT, $3); }
|	expr "<=" expr { $$ = new ASTOp($1, LE, $3); }
|	expr ">=" expr { $$ = new ASTOp($1, GE, $3); }
|	cond '&' cond { $$ = new ASTOp($1, AND, $3); }
|	cond '|' cond { $$ = new ASTOp($1, OR, $3); }
;

%%

void yyerror (const char *msg) {
	fatal("%s in \"%s\"\n", msg, yytext);
}

int main(int argc, char *argv[]) {
	filename = argv[1];
	linecount = 1;
	if (yyparse()) return 1;
	initSymbolTable(997);
	openScope();
	initLibFunctions();
	t->sem();
	closeScope();
	destroySymbolTable();
	if (sem_failed) return sem_failed;
	codegen(t);
	delete t;
	return 0;
}
